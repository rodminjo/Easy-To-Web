plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.backend'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// yrs4j
	implementation fileTree(dir: 'libs', includes: ['*.jar'])

	// webSocket
	implementation 'org.springframework.boot:spring-boot-starter-websocket'

	// database
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'com.github.codemonstur:embedded-redis:1.4.3'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'com.h2database:h2'

	// jwt
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

	// smtp
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

	// opencv for webp
	implementation 'org.openpnp:opencv:4.9.0-0'

	// micrometer for monitoring
	implementation 'io.micrometer:micrometer-registry-prometheus'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux' // webTestClient 사용을 위한 추가
	testImplementation 'com.icegreen:greenmail-junit5:2.0.1' // greenmail junit
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register("unitTest", Test) {
	useJUnitPlatform {
		includeTags "unitTest"
	}
	description = "Runs unit tests only."
	group = "verification"
}

tasks.register("mediumTest", Test) {
	useJUnitPlatform {
		includeTags "mediumTest"
	}
	description = "Runs medium tests only."
	group = "verification"
}

tasks.register("integrationTest", Test) {
	useJUnitPlatform {
		includeTags "integrationTest"
	}
	description = "Runs integration tests only."
	group = "verification"
}

// test 태스크 정의 및 실행 순서 지정
tasks.named("test") {
// 	병렬 실행 방지
//	maxParallelForks = 1
	description = "Runs all tests in alphabet order: integration -> medium -> unit"
	group = "verification"
	dependsOn("unitTest", "mediumTest", "integrationTest")
}

// buildpack 이용한 image 생성
bootBuildImage {
	imageName = 'easy-to-web'
	builder = 'paketobuildpacks/builder-jammy-full'
}
